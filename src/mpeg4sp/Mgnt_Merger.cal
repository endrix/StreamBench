// Mgnt_Merger420
//
// Author: Christophe Lucarz (christophe.lucarz@epfl.ch)
package mpeg4sp;

actor Mgnt_Merger() int(size=9) YUV_i ==> uint(size=8) YUV :

	// Select the Y blocks 
	Y: action YUV_i:[ y ] repeat 256 ==> YUV:[ out_y ] repeat 256
	var
		uint(size=8) out_y[256]
	do

		foreach int j in 0 .. 7 do
			foreach int i in 0 .. 7 do
				out_y[i + j * 16] := y[i + j * 8];
			end
		end
		foreach int j in 0 .. 7 do
			foreach int i in 0 .. 7 do
				out_y[i + 8 + j * 16] := y[i + 64 + j * 8];
			end
		end
		foreach int j in 0 .. 7 do
			foreach int i in 0 .. 7 do
				out_y[i + (j + 8) * 16] := y[i + 2 * 64 + j * 8];
			end
		end
		foreach int j in 0 .. 7 do
			foreach int i in 0 .. 7 do
				out_y[i + 8 + (j + 8) * 16] := y[i + 3 * 64 + j * 8];
			end
		end
	end

	// Select the U blocks 
	U: action YUV_i:[ u ] repeat 64 ==> YUV:[ u ] repeat 64
	end

	// Select the V blocks 
	V: action YUV_i:[ v ] repeat 64 ==> YUV:[ v ] repeat 64
	end

	schedule fsm Y :
		Y ( Y ) --> U;
		U ( U ) --> V;
		V ( V ) --> Y;
	end

end