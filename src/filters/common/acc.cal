// acc.cal -- part of 'DigitalFiltering' project
//
// Author: 
// Jani Boutellier (jani.boutellier@ee.oulu.fi)
// Department of Computer Science and Engineering
// University of Oulu, Finland
//
// Description:
// Accumulator
//
// Changelog:
// - initial version 27/09/2013
// - simplification 24/04/2014 (jboutell)

package filters.common;

import filters.common.constants.*;

actor acc(int initial_sample) int(size=SAMPLE_SZ) operand_1 ==> int(size=SAMPLE_SZ) result :

	int(size=SAMPLE_SZ) buffer;

	init: action ==> result:[ buffer ]
	do
		buffer := initial_sample;
	end

	run: action operand_1:[ x ] ==> result:[ res ]
	var
		int(size=SAMPLE_SZ) res
	do
		res := buffer + x;
		buffer := res;
	end
	
	schedule fsm s_init:
		s_init ( init ) --> s_run;
		s_run ( run ) --> s_run;
	end

end
